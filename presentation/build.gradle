apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    def ext = rootProject.extensions.ext
    compileSdkVersion ext.android.compileSdk

    defaultConfig {
        minSdkVersion ext.android.minSdk
        targetSdkVersion ext.android.targetSdk
        versionCode ext.application.versionCode
        versionName ext.application.versionName
        applicationId ext.application.id
        testInstrumentationRunner ext.application.testInstrumentationRunner
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
    }

    aaptOptions {
        cruncherEnabled !ext.build.ciBuild
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -DpreDex=false.
        preDexLibraries !ext.build.ciBuild && ext.build.preDexEnabled
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    dataBinding {
        enabled true
    }

    testOptions {
        unitTests {
            // By default, local unit tests throw an exception any time the code you are testing tries to access
            // Android platform APIs (unless you mock Android dependencies yourself or with a testing
            // framework like Mockito). However, you can enable the following property so that the test
            // returns either null or zero when accessing platform APIs, rather than throwing an exception.
            returnDefaultValues true
        }
        animationsDisabled true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':domain')
    implementation project(':data')
    kapt presentationKaptDependencies
    implementation presentationDependencies
    testImplementation presentationTestDependencies
    androidTestImplementation presentationInstrumentationTestDependencies
}
